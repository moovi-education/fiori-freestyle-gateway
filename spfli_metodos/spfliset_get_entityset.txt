  method SPFLISET_GET_ENTITYSET.

*   Variavel de importação do typo retorno do método get_filter_select_options( )
*   do metodo de retorno da método get_filter
*   da classe /IWBEP/IF_MGW_REQ_ENTITYSET
    DATA: filter_select_options TYPE  /iwbep/t_mgw_select_option,
          range_carrid          TYPE RANGE OF spfli-carrid,
          range_connid          TYPE RANGE OF spfli-connid,
          wa_carrid             LIKE LINE OF range_carrid,
          wa_connid             LIKE LINE OF range_connid.

*=================ASSOCIAÇÃOO=========================================
*    Esse é um parêtro que vem prenchido quando a busca
*    é feito pela associação
    READ TABLE it_key_tab ASSIGNING FIELD-SYMBOL(<f_tab_key>)
    WITH KEY name = 'Carrid'.
    IF sy-subrc EQ 0.
      wa_carrid-sign = 'I'.
      wa_carrid-option = 'EQ'.
      wa_carrid-low = <f_tab_key>-value.
      APPEND wa_carrid TO range_carrid.
    ENDIF.
*=================ASSOCIAÇÃOO=========================================

* $filter
    filter_select_options = io_tech_request_context->get_filter( )->get_filter_select_options( ).
    LOOP AT filter_select_options ASSIGNING FIELD-SYMBOL(<filter_select_options>).

      READ TABLE <filter_select_options>-select_options
      ASSIGNING FIELD-SYMBOL(<select_options>) INDEX 1.
      CASE <filter_select_options>-property.
*        Campo ABAP definido na sua entity que pode ser filtravel
        WHEN 'CARRID'.
*          Como a estrutura do field symbols <filter_select_options>
*          tem a estrutura de um range
          MOVE-CORRESPONDING <select_options> TO wa_carrid.
          APPEND wa_carrid TO range_carrid.
        WHEN 'CONNID'.
*          Como a estrutura do field symbols <filter_select_options>
*          tem a estrutura de um range
          MOVE-CORRESPONDING <select_options> TO wa_connid.
          APPEND wa_connid TO range_connid.

      ENDCASE.

    ENDLOOP.


    SELECT
       carrid,
       connid,
       countryfr,
       cityfrom,
       airpfrom,
       countryto,
       cityto,
       airpto,
       fltime,
       deptime,
       arrtime,
       distance,
       distid,
       fltype,
       period
      INTO TABLE @DATA(t_entityset)
      FROM spfli
      WHERE carrid IN @range_carrid
        AND connid IN @range_connid.

    MOVE-CORRESPONDING t_entityset TO et_entityset.


  endmethod.
